buildscript {
    ext {
        springBootVersion = '1.5.22.RELEASE'
        js2pVersion = '1.0.1'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
    id "no.nils.wsdl2java" version "0.10"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
    options.windowTitle = 'FINT Documaster Arkiv Adapter'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file('./javadocs/')
}

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}


sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://dl.bintray.com/fint/maven'
    }
}

dependencies {
    compile 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1'

    // enable extension support for wsdl2java
    wsdl2java 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1'
    wsdl2java 'org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1'

    compile('com.github.springfox.loader:springfox-loader:1.2.0')
    compile('com.google.guava:guava:28.1-jre')
    compile('org.apache.commons:commons-lang3:3.9')
    compile('org.apache.tika:tika-core:1.23')
    compile('org.glassfish.jersey.core:jersey-client:2.29.1')
    compile('org.glassfish.jersey.core:jersey-common:2.29.1')
    compile('org.jooq:jool-java-8:0.9.14')
    compile('org.projectlombok:lombok:1.18.10')
    compile('commons-beanutils:commons-beanutils:1.9.4')


    // Spring adds validation-api 1.x and hibernate-validator 5.x by default, all three dependencies needed to override.
    // Version 6.1.0 does not work :(
    compile 'javax.validation:validation-api:2.0.1.Final'
    compile 'org.hibernate.validator:hibernate-validator:6.0.17.Final'
    compile 'org.hibernate.validator:hibernate-validator-annotation-processor:6.0.17.Final'

    compile('no.fint:fint-sse:1.4.0')
    compile('no.fint:fint-event-model:3.0.0')
    compile('no.fint:fint-relation-model:1.1.5')
    compile('no.fint:fint-model-resource:0.3.3')

    compile('no.fint:fint-arkiv-case-defaults:2.0.0-rc-3')
    compile("no.fint:fint-arkiv-resource-model-java:${apiVersion}")
    compile("no.fint:fint-administrasjon-resource-model-java:${apiVersion}")

    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('org.springframework.boot:spring-boot-actuator')

    testCompile('cglib:cglib-nodep:3.3.0')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    compile 'com.azure:azure-storage-blob:12.1.0'
    compile 'io.projectreactor:reactor-core:3.3.1.RELEASE'
}

wsdl2java {
    generatedWsdlDir = file('generated-sources')
    wsdlDir = file("$projectDir/src/main/resources/wsdl")
    locale = Locale.ENGLISH
    wsdlsToGenerate = [
            ['-xjc-XtoString', '-client', '-b', "$projectDir/src/main/resources/wsdl/bindings-oms.xml", '-p', 'no.fint.arkiv.sikri.oms', '-autoNameResolution', "$projectDir/src/main/resources/wsdl/ObjectModelServiceV3En.wsdl"],
            ['-xjc-XtoString', '-client', '-exsh', 'true', '-b', "$projectDir/src/main/resources/wsdl/bindings-ds.xml", '-p', 'no.fint.arkiv.sikri.ds', "$projectDir/src/main/resources/wsdl/DocumentServiceV3.wsdl"],
    ]
    //stabilizeAndMergeObjectFactory = true
}

wsdl2javaExt {
    cxfVersion = '3.3.4'
    deleteGeneratedSourcesOnClean = true
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.6.0/dependencyReport.gradle'
